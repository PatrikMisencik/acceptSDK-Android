apply plugin: 'com.android.application'

repositories {
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "de.wirecard.accept.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 11
        versionName "1.11.1"
    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        all {
            def Properties appProps = new Properties()
            try {
                appProps.load(new FileInputStream(file("../../AcceptSDKAndroidDemo.properties")))
            } catch (IOException e) {
                appProps.put("clientID", System.getenv('BACKEND_DEFAULT_UID'))
                appProps.put("clientSecret", System.getenv('BACKEND_DEFAULT_SECRET'))
                appProps.put("apiPath", System.getenv('BACKEND_DEFAULT_URL'))
            }
            buildConfigField "String", "clientID", appProps.getProperty("clientID", "\"\"")
            buildConfigField "String", "clientSecret", appProps.getProperty("clientSecret", "\"\"")
            buildConfigField "String", "apiPath", appProps.getProperty("apiPath", "\"\"")
        }
    }

    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }
    productFlavors {
        flavor_thyron {
            applicationId 'de.wirecard.accept.sample.thyron'
        }
        flavor_bbpos {
            applicationId 'de.wirecard.accept.sample.bbpos'
        }
    }
}

dependencies {
     compile 'com.github.mposSVK:acceptSDK-Android:1.5.9'
     flavor_thyronCompile 'com.github.mposSVK:accept-android-extension-spire:1.5.8'
     flavor_bbposCompile 'com.github.mposSVK:accept-android-extension-bbpos:1.5.3'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.3'
    compile 'com.android.support:appcompat-v7:' + rootProject.ext.supportLib
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'com.squareup.picasso:picasso:2.5.2'

}
